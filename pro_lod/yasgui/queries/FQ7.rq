########### 
# FQ7. Federated Query: for protein 'EGFR' in ProKinO, get their proteoforms from PRO  
#	   that have a modification at site 768. 
#     (Liang-Chin Huang et al.) 
########### 
 
PREFIX prokino: <http://prokino.uga.edu/prokino#> 
PREFIX prokino.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX pro.oboInOwl: <http://www.geneontology.org/formats/oboInOwl#> 
PREFIX pro.owl: <http://www.w3.org/2002/07/owl#> 
PREFIX pro.paf: <http://pir.georgetown.edu/pro/paf#> 
PREFIX pro.pr: <http://purl.obolibrary.org/obo/pr#> 
PREFIX pro.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX pro.rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX nextprot: <http://nextprot.org/rdf#> 
PREFIX nextprot.db: <http://nextprot.org/rdf/db/> 
PREFIX nextprot.rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX mgi: <http://bio2rdf.org/mgi_vocabulary:> 
PREFIX mgi.affy: <http://bio2rdf.org/affymetrix_vocabulary:> 
PREFIX mgi.dcterms: <http://purl.org/dc/terms/> 
PREFIX mgi.go: <http://bio2rdf.org/goa_vocabulary:> 
PREFIX mgi.owl: <http://www.w3.org/2002/07/owl#> 
PREFIX mgi.rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
 
SELECT DISTINCT 
(STR(?genename) AS ?Protein)
(STR(?uniprotid) AS ?UniProt_ID) 
(STR(?query_position) AS ?Position)
(STR(?pro_organism_gene_id) AS ?PRO_Organism_Gene) 
(STR(?pro_gene_id) AS ?PRO_Gene) 
(STR(?proteoform) AS ?Proteoform) 
(STR(?proteoform_label) AS ?Label) 
(STR(?proteoform_std) AS ?Proteoform_STD)
FROM NAMED <http://prokino.uga.edu> 
FROM NAMED <http://purl.obolibrary.org/obo/pr> 
WHERE 
{ 
	#SPARQL Endpoint: PRO 
	SERVICE <http://sparql.proconsortium.org/virtuoso/sparql> 
	{ 
		GRAPH <http://purl.obolibrary.org/obo/pr> 
		{ 
			SELECT DISTINCT 
				?genename 
				?uniprotid 
				?query_position 
				#Replace "http://purl.obolibrary.org/obo/PR_XXXXXXXXX" with "PR:XXXXXXXXX" for PRO's ID 
      			(REPLACE(STRAFTER(STR(?pro_organism_gene),"/obo/"),"_",":") AS ?pro_organism_gene_id)
      			(REPLACE(STRAFTER(STR(?pro_gene),"/obo/"),"_",":") AS ?pro_gene_id) 
      			(REPLACE(STRAFTER(STR(?modification),"/obo/"),"_",":") AS ?proteoform) 
				?proteoform_label 
				?proteoform_std 
			WHERE 
			{ 
				#Define protein kinases by ProKinO and use UniProt ID (accession number) as backbone 
				{ 
					SELECT DISTINCT 
						?genename 
						?uniprotid 
						?pro_organism_gene	#Use PRO term (organism-gene) to connect the data obtained from PRO 
					WHERE 
					{ 
						SERVICE <http://vulcan.cs.uga.edu/sparql> 
						{ 
							GRAPH <http://prokino.uga.edu> 
							{ 
								SELECT DISTINCT 
									?genename 
									?uniprotid 
									#Add a prefix to a UniProt ID and let it be the organism gene ID in PRO 
                					(CONCAT("http://purl.obolibrary.org/obo/PR_",STR(?uniprotid)) AS ?pro_organism_gene) 
								WHERE 
								{ 
									#Define the genes of interest 
									#In low level query for a specific position, we only focus on one gene 
									VALUES ?query_gene {'EGFR'} 
									 
									#Get human protein kinases 
									?gene prokino.rdf:type prokino:Protein . 
									?gene prokino:presentIn prokino:Human . 
									?gene prokino:hasPrimaryName ?genename . 
									?gene prokino:hasDbXref ?protid . 
									FILTER(STR(?genename) = ?query_gene) 
									 
									#Get primary UniProt ID 
									?protid prokino:isPrimaryUniprotId ?isprimaryuniprotid . 
									?protid prokino:hasIdentifier ?uniprotid . 
									#A protein also has secondary accession numbers, so it needs to be filtered 
									FILTER REGEX(?isprimaryuniprotid,"true") 
								} 
							} 
						} 
					} 
				} 
				#Appending Information: PRO 
				OPTIONAL 
				{ 
					#Use PRO term (gene) to connect the data obtained from PRO 
					{ 
						SELECT DISTINCT 
							?pro_organism_gene 
							?pro_gene 
						WHERE 
						{ 
							?pro_organism_gene pro.rdfs:comment ?category . 
							?pro_organism_gene pro.rdfs:subClassOf ?pro_gene . 
							 
							?pro_gene pro.rdfs:comment ?parentcategory . 
							FILTER REGEX(?parentcategory,"Category=gene") 
						} 
					} 
					#PRO organism-modification 
					OPTIONAL 
					{ 
						SELECT DISTINCT 
							?pro_gene 
							?query_position 
							?modification 
							?proteoform_label 
							?proteoform_std 
						WHERE 
						{ 
							#Define mutation position 
							VALUES ?query_position {'768'} 
 
							?modification pro.rdfs:comment ?category . 
							FILTER REGEX(?category,"Category=organism-modification") 
 
							?modification pro.rdfs:subClassOf+ ?pro_gene . 
							?pro_gene pro.rdfs:comment ?parentcategory . 
							FILTER REGEX(?parentcategory,"Category=gene") 
							 
							#Description of the Proteoform 
							OPTIONAL 
							{ 
								?mod a pro.owl:Axiom . 
								?mod pro.oboInOwl:hasSynonymType pro.pr:PRO-proteoform-std . 
								?mod pro.owl:annotatedSource ?modification . 
								?mod pro.owl:annotatedTarget ?proteoform_std . 
							} 
							 
							?modification pro.rdfs:label ?proteoform_label . 
							 
							#Search the pattern "-POS" in proteoform annotation 
							BIND(CONCAT("-",STR(?query_position)) AS ?query_position_str) . 
							FILTER REGEX(?proteoform_std,?query_position_str) 
						} 
					} 
				} 
			} 
		} 
	} 
} 
ORDER BY ?genename ?proteoform_label 
